plugins {
    id "com.palantir.git-version" version "3.0.0"
    id "serviceannotations.java-conventions"
    id "serviceannotations.deps-conventions"
    id "serviceannotations.publishing-conventions"
}

def versionLabel(gitInfo) {
    def branch = gitInfo.branchName // all branches are snapshots, only tags get released
    def tag = gitInfo.lastTag
    // tag is returned as is. Branch may need cleanup
    return branch == null ? tag : branch.replace("/","-") + "-SNAPSHOT"
}

allprojects {
    group = 'mil.army.usace.hec'
    version = versionLabel(versionDetails())
}

jar {
    manifest {
        attributes('Automatic-Module-Name': 'rma.services.annotations.serviceannotations')
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}


